/*
Copyright 2018 Sam Pritchard

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
 */

buildscript {
    ext {
        dokka_version = '0.9.17'
        shadow_version = '2.0.4'
        kotlin_version = '1.3.0-rc-198'
        versions_version = '0.20.0'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'application'

group 'basalt'
version '3.1.1'
description = 'A high-performance, distributed audio node server based on Lavaplayer, Magma and Undertow.'
sourceCompatibility = 10
targetCompatibility = 10
mainClassName = 'basalt.MainKt'

repositories {
    jcenter()
    maven {url 'https://jitpack.io'}
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    moduleName = 'Basalt'
    outputFormat = 'html'
    outputDirectory = javadoc.destinationDir
    jdkVersion = 8
    includeNonPublic = true

    linkMapping {
        dir = 'src/main/kotlin'
        url = 'https://github.com/SamOphis/Basalt/blob/master/src/main/kotlin'
        suffix = '#L'
    }

    packageOptions {
        prefix = 'basalt.messages'
        suppress = true
    }

    includes = ['basalt.md']

    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task updateDocs(type: Copy, dependsOn: javadocJar) {
    from "${buildDir}/docs/javadoc"
    into "docs"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

shadowJar {
    classifier = 'withDependencies'
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn sourcesJar
    dependsOn javadocJar
    dependsOn shadowJar
    dependsOn updateDocs
    dependsOn test

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
    shadowJar.mustRunAfter sourcesJar
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    vertx_version = '3.5.4' // v.x framework
    magma_version = '0.6.1' // better jda-audio
    jda_async_provider_version = '1.3' // async packet provider
    jda_native_audio_send_version = '1.0.6' // native audio send
    lavaplayer_version = '1.3.9' // lavaplayer
    jsoniter_version = '0.9.23' // json encoding/decoding
    jackson_yaml_version = '2.9.7' // parsing config file
    sentry_logback_version = '1.7.13' // logging
    javassist_version = '3.23.1-GA' // codegen for jsoniter
    kotlin_coroutines_version = '1.0.0-RC1' // coroutines in kotlin
    commons_lang3_version = '3.8.1' // for jda async packet provider :/
    oshi_version = '3.9.1' // stats
    trove_version = '3.0.3' // primitive collections
}

dependencies {
    compile group: 'space.npstr', name: 'Magma', version: magma_version
    compile group: 'io.vertx', name: 'vertx-lang-kotlin', version: vertx_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    compile group: 'com.github.shredder121', name: 'jda-async-packetprovider', version: jda_async_provider_version
    compile group: 'com.sedmelluq', name: 'jda-nas', version: jda_native_audio_send_version
    compile group: 'com.sedmelluq', name: 'lavaplayer', version: lavaplayer_version
    compile group: 'com.jsoniter', name: 'jsoniter', version: jsoniter_version
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jackson_yaml_version
    compile group: 'io.sentry', name: 'sentry-logback', version: sentry_logback_version
    compile group: 'org.javassist', name: 'javassist', version: javassist_version
    compile group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang3_version
    compile group: 'com.github.oshi', name: 'oshi-core', version: oshi_version
    compile group: 'net.sf.trove4j', name: 'trove4j', version: trove_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: kotlin_coroutines_version
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}